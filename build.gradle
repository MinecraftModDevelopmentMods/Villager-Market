buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://repo.maven.apache.org/maven2" }
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:${project.cursegradle_version}"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: "maven-publish"
apply plugin: "maven"

archivesBaseName = project.mod_name.replace(' ', '-').toLowerCase()
group = "${project.group_name}"

version = "${project.minecraft_version}-${project.mod_version}"
if (project.hasProperty('mod_suffix')) {
    version += "-${project.mod_suffix}"
}

compileJava { sourceCompatibility = targetCompatibility = "1.8" }

minecraft {
    version = "${project.minecraft_version}-${project.forge_version}"
    mappings = "${project.mcp_version}"
    runDir = "run"

    replace '@MOD-ID@', project.mod_id
    replace '@MOD-NAME@', project.mod_name
    replace '@MOD-VERSION@', project.mod_version
    replace '@MOD-MC-VERSION@', project.minecraft_version
    replace '@FORGE-VERSION@', project.forge_version
    replace '@FINGERPRINT@', project.findProperty('signSHA1')

    replaceIn 'Constants.java'

    if (project.hasProperty('mc_username')) {
        clientRunArgs += ['--username', "${project.mc_username}"]
        if (project.hasProperty('mc_password')) {
            clientRunArgs += ['--password=', "${project.mc_password}"]
        }
    }
    if (project.hasProperty('mc_uuid')) {
        clientRunArgs += ['--uuid', "${project.mc_uuid}"]
    }

    // disable server gui
    serverRunArgs += "nogui"

    // skip the screen to confirm that you want to load a world with missing registry entries
    serverJvmArgs += "-Dfml.doNotBackup=true"
    clientJvmArgs += "-Dfml.doNotBackup=true"

    // skip having to confirm on server
    serverJvmArgs += "-Dfml.queryResult=confirm"

    // skip jansi warnings in the log
    serverJvmArgs += "-Dlog4j.skipJansi=true"
    clientJvmArgs += "-Dlog4j.skipJansi=true"

    if (project.hasProperty('client_args')) {
        clientJvmArgs += "${project.client_args}"
    }
    if (project.hasProperty('server_args')) {
        serverJvmArgs += "${project.server_args}"
    }
}

import groovy.json.JsonSlurper

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ''
    }
}

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file 'secret.json'
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { name='mmd'; url='https://maven.mcmoddev.com' }

    maven { name='jei'; url="http://dvs1.progwml6.com/files/maven" }
}

dependencies {
    runtime "mezz.jei:jei_${project.minecraft_version}:${project.jei_version}+"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "modid", project.mod_id
    inputs.property "modname", project.mod_name

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'modid': project.mod_id, 'modname': project.mod_name
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

jar {
    from sourceSets.api.output
    from sourceSets.main.output
}

task sourcesJar(type: Jar) {
    from sourceSets.api.allSource
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.api.output
    from sourceSets.main.output
    classifier = 'deobf'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

    // findProperty allows us to reference the property without it existing.
    // Using project.propName would cause the script to fail validation if
    // the property did not exist.
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar
tasks.publish.dependsOn build
tasks.publish.finalizedBy curseforge

artifacts {
    archives sourceJar, deobfJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            def versionForMaven = "$project.minecraft_version-$project.mod_version"
            if (System.getenv().BUILD_NUMBER) {
                versionForMaven += '.' + System.getenv().BUILD_NUMBER
            }
            version versionForMaven
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact deobfJar {
                classifier 'deobf'
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    if (project.hasProperty('cf_project_id') && project.hasProperty('cf_release_type') &&
            ((project.cf_release_type == "alpha") || (project.cf_release_type == "beta") || (project.cf_release_type == "release"))) {
        apiKey = secret.curseforgeAPIKey
        project {
            id = "$project.cf_project_id"
            changelog = file('current-changelog.txt')
            releaseType = project.cf_release_type
            addGameVersion(project.minecraft_version)
            def projectName = project.mod_name
            def displayVersion = project.mod_version
            if (System.getenv().BUILD_NUMBER) {
                displayVersion += '.' + System.getenv().BUILD_NUMBER
            }
            mainArtifact(jar) {
                displayName = "$project.mod_name : $displayVersion"
            }
            addArtifact(sourceJar) {
                displayName = "$project.mod_name : $displayVersion : Sources"
            }
            addArtifact(deobfJar) {
                displayName = "$project.mod_name : $displayVersion : Development"
            }
        }
    }
}
